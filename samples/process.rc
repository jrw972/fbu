// From Distributed Algorithms, p. 204.

package samples;

type Element struct
{
  value int;
  next *Element;
};

type Queue struct
{
  head *Element;
  tail **Element;
};

func (this *Queue) Push (v int)
{
  if this.head == nil
    {
      this.tail = &this.head;
    };

  var e *Element;
  e = new (Element);
  e.value = v;
  *this.tail = e;
  this.tail = &e.next;
};

func (this *Queue) Pop ()
{
  this.head = this.head.next;
  if this.head == nil
    {
      this.tail = &this.head;
    };
};

func (this $const * Queue) Empty () bool
{
  return this.head == nil;
};

func (this $const * Queue) Front () int
{
  return this.head.value;
};

type Channel component
{
  queue Queue;
  receive push (message int);
};

reaction (this $const * Channel) send (message int)
{
  activate
  {
    this.queue.Push (message);
  };
};

action (this $const * Channel) _receive (!this.queue.Empty ())
{
  activate receive (this.queue.Front ())
    {
      this.queue.Pop ();
    };
};

// From Distributed Algorithms, p. 205.

type Process component
{
  i int;
  val [3]int;
  send [3] push (v int);
  decide push (v int);
};

init (this *Process) Init (i int)
{
  println (`I am process `, i);
  this.i = i;
};

reaction (this $const * Process) Initr (v int)
{
  activate
  {
    this.val[this.i] = v;
    println (`Process `, this.i, ` initialized with value `, v);
  };
};

[3] action (this $const * Process) _send (this.val[this.i] != 0 && this.i != IOTA)
{
  // send
  println (`Process `, this.i, ` sending `, this.val[this.i], ` to process `, IOTA);
  activate send[IOTA] (this.val[this.i])
  { };
};

[3] reaction (this $const * Process) receive (v int)
{
  println (`In receive `, this);
  println (`Process `, this.i, ` received `, v, ` from process `, IOTA);
  activate
  {
    this.val[IOTA] = v;
  };
};

func p (val [3]int) bool {
  var i int;
  i = 0;
  for i != 3 {
    if val[i] == 0 {
      return false;
    };
    i++;
  };

  return true;
};

func f (val [3]int) int
{
  var i, sum int;
  i = 0;
  sum = 0;
  for i != 3 {
    sum += val[i];
    i++;
  };
  return sum;
};

action (this $const * Process) _decide (p (this.val))
{
  // decide
  var v int;
  v = f (this.val);
  println (`Process `, this.i, ` decided the value is `, v);
  activate decide (v)
  { };
};

type System component
{
  process [3]Process;
  channel [3][3]Channel;
  initp [3] push (v int);
};

init (this *System) Init ()
{
  var i int;
  i = 0;
  for i != 3 {
    this.process[i].Init (i);
    i++;
  };
};

[3] action (this $const * System) _send (true)
{
  // send
  activate initp[IOTA] (100 + IOTA)
  { };
};

[3] reaction (this $const * System) decide (v int)
{
  println (`Process `, IOTA, ` decided the value is `, v);
};

bind (this *System) _bind {
  for i ... 3 {
    this.initp[i] -> this.process[i].Initr;
    this.process[i].decide -> this.decide ... i;

    for j ... 3 {
      if i != j {
        this.process[i].send[j] -> this.channel[i][j].send;
        this.channel[i][j].receive -> this.process[j].receive ... i;
      };
    };
  };
};

instance system System Init ();
